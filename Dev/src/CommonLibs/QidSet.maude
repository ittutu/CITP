***( SpeX/CommonLibs

  This file is part of CommonLibs (CL), a suite of Maude 3 libraries
  that extend the predefined data modules from the standard prelude.

  Copyright : (C) 2020, 2021 Simion Stoilow Institute of Mathematics
              of the Romanian Academy (IMAR)
  License   : GNU General Public License v2.0 or later (GPL-2.0-or-later)
  Author    : Ionut Tutu <Ionut.Tutu@imar.ro>

)

sload ./QidList.maude
sload ./Term.maude

********************************************************************************

fmod QID-SET/OPS is
  protecting TERM/OPS .
  protecting QID-LIST/OPS .
  protecting QID/READ .

  var QI : Qid .
  var QIL : QidList .
  var QIS : QidSet .

  *** _not-in_  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op _not-in_ : Qid QidSet -> Bool .
  eq QI not-in QIS = not (QI in QIS) .

  *** _included-in_  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op _included-in_ : QidList QidSet -> Bool .
  eq nil included-in QIS = true .
  eq (QI QIL) included-in (QI ; QIS) = QIL included-in (QI ; QIS) .
  eq (QI QIL) included-in QIS = false [owise] .

  *** first-not-in  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op first-not-in : QidList QidSet -> QidList .
  eq first-not-in(nil, QIS) = nil .
  eq first-not-in(QI QIL, QIS)
   = if QI not-in QIS
     then QI
     else first-not-in(QIL, QIS) fi .

  *** filter-in  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op filter-in : QidList QidSet -> QidList .
  eq filter-in(nil, QIS) = nil .
  eq filter-in(QI QIL, QIS)
   = if QI in QIS then QI else nil fi filter-in(QIL, QIS) .

  *** list  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op list : QidSet -> QidList .
  eq list(none) = nil .
  eq list(QIS) = read-qids(unpack('_;_, upTerm(QIS))) [owise] .

  *** cs-list  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op cs-list : QidSet -> QidList .
  eq cs-list(QIS) = intersperse('`,, list(QIS)) .

  *** set  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op set : QidList -> QidSet .
  eq set(nil) = none .
  eq set(QI QIL) = QI ; set(QIL) .
endfm

********************************************************************************
