***( SpeX/Lang/CITP

  This file is part of CITP.

  License : GNU General Public License v2.0 or later (GPL-2.0-or-later)
  Authors : Daniel Gaina <daniel@imi.kyushu-u.ac.jp>,
            Ionut Tutu <Ionut.Tutu@imar.ro>,
            Adrian Riesco <ariesco@fdi.ucm.es>

)

sload ./Data.maude

********************************************************************************

fmod CITP/SPEC-LANG/BASE is
  protecting CITP/LID .
  protecting SPEX/DATABASE .
  protecting META-LEVEL .

  op _is`known_spec`lang : QidList CITP -> Bool .
  eq S:QidList is known L:CITP spec lang = false [owise] .

  op get-module : CITP QidList Qid SpeX/DB ~> Module .
endfm

*** ----------------------------------------------------------------------------

fth CITP/SPEC-LANG is
  including CITP/SPEC-LANG/BASE .

  op lang-name : -> QidList .
  op get-module : Qid SpeX/DB ~> Module .
endfth

*** ----------------------------------------------------------------------------

fmod CITP/REGISTER { SL :: CITP/SPEC-LANG } is
  var L : CITP .
  var S : QidList .
  var MN : Qid .
  var DB : SpeX/DB .

  ceq S is known L spec lang = true if S = lang-name .
  ceq get-module(L, S, MN, DB) = get-module(MN, DB) if S = lang-name .
endfm

********************************************************************************

view CITP/SPEC-LANG/Maude from CITP/SPEC-LANG to CITP/SPEC-LANG/BASE is
  op lang-name to term 'Maude .
  op get-module(MN:Qid, DB:SpeX/DB) to term upModule(MN:Qid, true) .
endv

********************************************************************************

mod CITP/SPEC-LANG/ALL is
  including CITP/REGISTER { CITP/SPEC-LANG/Maude } .
endm

********************************************************************************
