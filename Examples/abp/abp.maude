

*** The protocol consists of two processes, Sender and Receiver, each having a data buffer and a one-bit state.
*** Sender and Receiver use two channels to communicate with each other, as they do not share any common memory:
*** a) a data channel from Sender to Receiver for sending bit-packet pairs, and
*** b) an acknowledgement channel from Receiver to Sender for sending single bits for confirmation.

*** The protocol works as follows:
*** - Initially, both channels are empty and Sender's bit is different from Receiver's bit.
*** - Sender repeatedly writes bit-packet pairs [bit, pac] to the data channel,
***   where bit is the Sender's bit and pac is the Sender's data to transmit.
***   If the Sender receives bit from Receiver over the acknowledgement channel,
***   then that is a confirmation that the packet sent has been delivered.
***   In that case, Sender flips its bit and select the next packet to send.

*** - Receiver writes its bit to the acknowledgement channel repeatedly.
*** When Receiver reads a pair [bit, pac] such that bit is different from its bit,
*** then it stores pac and flips its bit.

--- ---------------------------------------------
--- ---------------------------------------------
fmod EQUAL is
 op _=?_ :  Universal Universal -> Bool  [comm prec 51 poly (1 2) metadata "equality"].
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod SEQUENCE{X :: TRIV} is
 inc EQUAL .
 sort Sequence .
 subsorts X$Elt < Sequence .
--- constructors 
 op empty : -> Sequence [ctor] .
 op _`,_ : Sequence Sequence -> Sequence [ctor id: empty assoc] .
---
 vars E E1 E2 : X$Elt .
 vars Q Q1 Q2 : Sequence .
--- negation ---
 eq (Q1,E,Q2 =? empty) = false .
--- equality predicate ---
 eq Q =? Q = true .
 eq E1,Q1 =? E2,Q2 = (E1 =? E2) and (Q1 =? Q2) .
--- matching predicate Q1,E,Q2 := Q
 op _[]_:=_ : Sequence Sequence Sequence -> Bool . 
 eq Q1 [] Q2 := empty = false .
 eq empty [] Q2 := E,Q = Q2 =? Q .
 eq E1,Q1 [] Q2 := E,Q  = (E1 =? E) and Q1 [] Q2 := Q  .
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod PAIR{X :: TRIV, Y :: TRIV} is
 sort Pair .
 --- 
 op [_,_] : X$Elt Y$Elt -> Pair [ctor] .
 op fst : Pair -> X$Elt .
 op snd : Pair -> Y$Elt .
 --- 
 vars A A' : X$Elt .
 vars B B' : Y$Elt .
 --- 
 eq fst([ A,B ]) = A .
 eq snd([ A,B ]) = B .
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod PACKET is inc NAT .
 sort Packet .
 op pac : Nat -> Packet [ctor] .
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod BIT is
 inc EQUAL .
 sort Bit .
 op t : -> Bit [ctor] .
 op f : -> Bit [ctor] .
 op not_ : Bit -> Bit .
 eq not f = t .
 eq not t = f .
---
 var B : Bit .
 eq  not not B = B .
--- negations ---
 eq (t =? f) = false .
 eq ((not B) =? B) = false [nonexec].
endfm
--- ---------------------------------------------
--- ---------------------------------------------
view TRIV2BIT from TRIV to BIT is 
 sort Elt to Bit .
endv

view TRIV2PACKET from TRIV to PACKET is 
 sort Elt to Packet .
endv

fmod BIT-PACKET-PAIR is 
 inc PAIR{TRIV2BIT, TRIV2PACKET} * (sort Pair to BPPair) . 
endfm

view TRIV2BIT-PACKET-PAIR from TRIV to BIT-PACKET-PAIR is
 sort Elt to BPPair .
endv

fmod CHANNEL1 is 
 pr SEQUENCE{TRIV2BIT-PACKET-PAIR} * (sort Sequence to Channel1) .
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod CHANNEL2 is
 pr SEQUENCE{TRIV2BIT} * (sort Sequence to Channel2) .
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod PACKET-LIST is
 pr SEQUENCE{TRIV2PACKET} * 
  (sort Sequence to List,
   op empty to nil) .
 op mk : Nat -> List .
 var N : Nat .
 eq mk(0) = pac(0),nil .
 eq mk(s N) = pac(s N), mk(N) . 
endfm
--- ---------------------------------------------
--- ---------------------------------------------
fmod SYSTEM is 
 sort Sys .
--- initial state ---
 op init : -> Sys [ctor].
--- constructors ----
 op rec1  : Sys -> Sys [ctor] . --- Sender's receiving bits
 op rec2  : Sys -> Sys [ctor] . --- Receiver's receiving pairs of bits&packets
 op send1 : Sys -> Sys [ctor] . --- Sender's sending pairs of bits&packets
 op send2 : Sys -> Sys [ctor] . --- Receiver's sending bits
 op drop1 : Sys -> Sys [ctor] . --- dropping one element of channel1
 op drop2 : Sys -> Sys [ctor] . --- dropping one element of channel2
endfm
--- ---------------------------------------------
*** ABP has loose semantics ***
--- ---------------------------------------------
fth ABP is
 pr SYSTEM .
 pr CHANNEL1 .
 pr CHANNEL2 .
 pr PACKET-LIST .
--- observations ---
  op channel1 : Sys -> Channel1 .  --- Sender-to-Receiver channel
  op channel2 : Sys -> Channel2 .  --- Receiver-to-Sender channel
  op bit1  : Sys -> Bit  .         --- Sender's bit
  op bit2  : Sys -> Bit  .         --- Receiver's bit
  op next  : Sys -> Nat  .         --- the packet number sent next by Sender
  op list  : Sys -> List  .        --- the packets received by Receiver
--- variables ---
  var S : Sys .
  vars B : Bit .
  vars C2 C2' : Channel2 . 
  vars C1 C1' : Channel1 . 
  var N : Nat .
  vars P : Packet .
--- init -----------------------------------------------------------------
  eq channel1(init) = empty .
  eq channel2(init) = empty .
  eq bit1(init)  = f .
  eq bit2(init)  = t .
  eq next(init)  = 0 .
  eq list(init)  = nil .
--- rec1 -----------------------------------------------------------------
 eq channel1(rec1(S)) = channel1(S) .
ceq channel2(rec1(S)) = channel2(S) if channel2(S) = empty[metadata "ca:c2-rec1-a"].
ceq channel2(rec1(S)) = C2          if B,C2 := channel2(S)[metadata "ca:c2-rec1-b"].
ceq bit1(rec1(S)) = bit1(S)     if channel2(S) = empty                   [metadata "ca:bit1-rec1-a"].
ceq bit1(rec1(S)) = bit1(S)     if B,C2 := channel2(S) /\ B = not bit1(S)[metadata "ca:bit1-rec1-b"].
ceq bit1(rec1(S)) = not bit1(S) if B,C2 := channel2(S) /\ B = bit1(S)    [metadata "ca:bit1-rec1-c"].
 eq bit2(rec1(S))  = bit2(S) .
ceq next(rec1(S)) = next(S)   if channel2(S) = empty                   [metadata "ca:next-rec1-a"].
ceq next(rec1(S)) = next(S)   if B,C2 := channel2(S) /\ B = not bit1(S)[metadata "ca:next-rec1-b"].
ceq next(rec1(S)) = s next(S) if B,C2 := channel2(S) /\ B = bit1(S)    [metadata "ca:next-rec1-c"].
 eq list(rec1(S)) = list(S) .
--- rec2 -----------------------------------------------------------------
ceq channel1(rec2(S)) = channel1(S) if channel1(S) = empty     [metadata "ca:c1-rec2-a"].
ceq channel1(rec2(S)) = C1          if [B,P],C1 := channel1(S) [metadata "ca:c1-rec2-b"].
 eq  channel2(rec2(S)) = channel2(S) .
 eq  bit1(rec2(S))  = bit1(S) .
ceq bit2(rec2(S))  = bit2(S)     if channel1(S) = empty                       [metadata "ca:bit2-rec2-a"].
ceq bit2(rec2(S))  = bit2(S)     if [B,P],C1 := channel1(S) /\ B = bit2(S)    [metadata "ca:bit2-rec2-b"].
ceq bit2(rec2(S))  = not bit2(S) if [B,P],C1 := channel1(S) /\ B = not bit2(S)[metadata "ca:bit2-rec2-c"].

---
 eq next(rec2(S))  = next(S) .
ceq list(rec2(S)) = list(S)   if channel1(S) = empty                       [metadata "ca:next-rec2-a"].
ceq list(rec2(S)) = list(S)   if [B,P],C1 := channel1(S) /\ B = bit2(S)    [metadata "ca:next-rec2-b"].
ceq list(rec2(S)) = P,list(S) if [B,P],C1 := channel1(S) /\ B = not bit2(S)[metadata "ca:next-rec2-c"].
--- send1 -----------------------------------------------------------------
  eq channel1(send1(S)) = channel1(S),[bit1(S),pac(next(S))] .
  eq channel2(send1(S)) = channel2(S) .
  eq bit1(send1(S))  = bit1(S) .
  eq bit2(send1(S))  = bit2(S) .
  eq next(send1(S))  = next(S) .
  eq list(send1(S))  = list(S) .
--- send2 -----------------------------------------------------------------
 eq channel1(send2(S)) = channel1(S) .
 eq channel2(send2(S)) = channel2(S),bit2(S) .
 eq bit1(send2(S))  = bit1(S) .
 eq bit2(send2(S))  = bit2(S) .
 eq next(send2(S))  = next(S) .
 eq list(send2(S))  = list(S) .
--- --- --- --- --- --- ---
--- undefined functions ---
--- --- --- --- --- --- ---
 ops x1 y1 : Sys -> Channel1 .
 ops x2 y2 : Sys -> Channel2 .
--- --- --- --- --- --- ---
--- drop1 -----------------------------------------------------------------
ceq channel1(drop1(S)) = x1(S),y1(S) if x1(S),[B,P],y1(S) := channel1(S)     [metadata "ca:c1-drop1-a"].
ceq channel1(drop1(S)) = channel1(S) if x1(S) [] y1(S) := channel1(S) = false[metadata "ca:c1-drop1-b"]. 
 eq channel2(drop1(S)) = channel2(S) .
 eq bit1(drop1(S))  = bit1(S) .
 eq bit2(drop1(S))  = bit2(S) .
 eq next(drop1(S))  = next(S) .
 eq list(drop1(S))  = list(S) .
--- drop2 -----------------------------------------------------------------
 eq channel1(drop2(S)) = channel1(S) .
ceq channel2(drop2(S)) = x2(S),y2(S) if x2(S),B,y2(S):= channel2(S)        [metadata "ca:c2-drop2-a"].
ceq channel2(drop2(S)) = channel2(S) if x2(S)[] y2(S):= channel2(S) = false[metadata "ca:c2-drop2-b"]. 
 eq bit1(drop2(S))  = bit1(S) .
 eq bit2(drop2(S))  = bit2(S) .
 eq next(drop2(S))  = next(S) .
 eq list(drop2(S))  = list(S) .
--- --- ---
endfth